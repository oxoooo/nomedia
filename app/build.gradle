/*
 * .nomedia - Hide annoying directories from your Gallery
 * Copyright (C) 2017 XiNGRZ <chenxingyu92@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

String git(String args) {
    return "git $args".execute([], project.rootDir).text.trim()
}

def gitHash = git('rev-parse --short HEAD')
def gitCommits = git('rev-list --count HEAD')
def gitTag = git('tag').split("\n").last()

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "ooo.oxo.apps.nomedia"

        minSdkVersion 21
        targetSdkVersion 26

        manifestPlaceholders = [APP_NAME: '@string/app_name']

        if (gitCommits.isEmpty()) {
            versionCode 0
        } else {
            versionCode Integer.parseInt(gitCommits)
        }

        if (gitTag.isEmpty()) {
            versionName "HEAD-$gitHash"
        } else {
            if (git("rev-parse --short $gitTag") != gitHash) {
                versionName "$gitTag-$gitHash"
            } else {
                versionName gitTag
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [APP_NAME: '@string/app_name_debug']
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.github.bumptech.glide:glide:4.3.0'
}
